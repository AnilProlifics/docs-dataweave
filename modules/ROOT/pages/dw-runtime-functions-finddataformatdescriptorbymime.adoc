= findDataFormatDescriptorByMime
ifndef::env-site,env-github[]
include::_attributes.adoc[]
endif::[]



[[finddataformatdescriptorbymime1]]
== findDataFormatDescriptorByMime&#40;mime: dw::module::Mime::MimeType&#41;: DataFormatDescriptor &#124; Null

Returns the `DataFormatDescriptor` based on the `dw::module::Mime::MimeType` specified or `null` if
there is no `DataFormatDescriptor` for the given `MimeType`.

_Experimental:_ This function is an experimental feature that is subject to change or removal from future versions of DataWeave.

_Introduced in DataWeave version 2.7.0._

=== Parameters

[%header, cols="1,1,3"]
|===
| Name | Type | Description
| `mimeType` | `dw::module::Mime::MimeType` | The MIME type value to search.
|===

=== Example

This example searches for a JSON `DataFormatDescriptor` and an unknown `DataFormatDescriptor`.

==== Source

[source,DataWeave,linenums]
----
%dw 2.0
import * from dw::Runtime
output application/json

var jsonDF = findDataFormatDescriptorByMime({'type': "*", subtype: "json", parameters: {}})
var unknownDF = findDataFormatDescriptorByMime({'type': "*", subtype: "*", parameters: {}})

fun simplify(df: DataFormatDescriptor | Null) = df  match {
  case d is DataFormatDescriptor -> { name: d.name, defaultMimeType: d.defaultMimeType }
  case is Null -> { name: "unknown", defaultMimeType: "unknown" }
}
---
{
  json: simplify(jsonDF),
  unknown: simplify(unknownDF)
}
----

==== Output

[source,Json,linenums]
----
{
  "json": {
    "name": "json",
    "defaultMimeType": "application/json"
  },
  "unknown": {
    "name": "unknown",
    "defaultMimeType": "unknown"
  }
}
----

